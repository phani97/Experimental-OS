alias physicalsp S0;
physicalsp=([PTBR +(SP/512)*2]*512)+(SP%512);  //getting physical address of SP
alias syscall S1;
syscall = [physicalsp-1]; //getting syscall from Stack

if(syscall==6) then //syscall is seek

	alias newseek S2;
	alias filedescriptor S3;
	
	newseek= [physicalsp-3];
	filedescriptor= [physicalsp-4];
	
	if(filedescriptor<0 || filedescriptor > 7) //checking filedescriptor is valid r not
	then 
	[physicalsp-2]=-1;
	ireturn;
	endif;
	
	alias pid S5;
	pid = (PTBR - 1024)/8;
	alias systemindex S4;
	systemindex=[READY_LIST + pid*32 +15 + filedescriptor*2];
	if(systemindex==-1)
	then 
	[physicalsp-2]=-1;
	ireturn;
	endif;
	
	alias fatindex S6;  //fatindex in systemwide open file table
	fatindex = [FILE_TABLE + systemindex*2];
	
	alias filesize S7;
	filesize = [FAT + fatindex*8 +1];
	
	if(newseek >= filesize) then //checking condition for out of bounds
	[physicalsp-2]=-1;
	ireturn;
	endif;
	
	[READY_LIST + pid*32 +15 + filedescriptor*2+1]=newseek;
	[physicalsp-2]=0;
	ireturn;
	
	else //syscall is read....
	
	alias filedescriptor S2;
	alias wordread S3;
	
	filedescriptor=[physicalsp-4];
	wordread=[physicalsp-3];
	
	if(filedescriptor<0 || filedescriptor > 7) //checking filedescriptor is valid r not
	then 
	[physicalsp-2]=-1;
	ireturn;
	endif;
	
	alias pid S5;
	pid = (PTBR - 1024)/8;
	alias systemindex S4;
	alias lseek S8;
	systemindex=[READY_LIST + pid*32 +15 + filedescriptor*2];
	lseek=[READY_LIST + pid*32 +16 + filedescriptor*2];
	if(systemindex==-1) //file is not opened.....!!
	then 
	[physicalsp-2]=-1;
	ireturn;
	endif;
	
	alias fatindex S6;
	fatindex=[FILE_TABLE+systemindex*2];
	
	alias basicblock S7;
	basicblock=[FAT+fatindex*8+2];
	
	load(1,basicblock);//loading basicblock;
	
	alias datablock S9;
	datablock=[SCRATCHPAD+lseek/512];
	
	if(datablock==-1) then //datablock not found
		
		
		[physicalsp-2]=-1;
		ireturn;
	endif;
		
		
	
	load(1,datablock); //loading datablock
	
	[physicalsp-3]=[SCRATCHPAD+lseek%512]; //wriring word
	
	
	
	[READY_LIST + pid*32 +16 + filedescriptor*2]=lseek+1; //incrementing pointer
	
	[physicalsp-2]=0;
	ireturn;
	
endif;
